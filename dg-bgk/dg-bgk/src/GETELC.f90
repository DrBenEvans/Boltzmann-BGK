      SUBROUTINE GETELC(NELEM_PP ,GEOME_PP ,NGEOM,NNODE,VNPNT,IV,& 
     &                  DISNF_PP,FLUXP_PP ,FLUYP_PP ,UMEAN_PP ,& 
     &                  RHS_PP,DTE ,UX,UY,MU,NFO_PP,ITIME,&
     &                  VSPACE_FIRST,VSPACE_LAST) 
! 
! *** THIS SUB. COMPUTES THE ELEMENT CONTRIBUTIONS 
! 
      INTEGER NELEM_PP,IE,IN,maxNELEM_PP,NGEOM,NNODE 
      INTEGER VNPNT,IV,ITIME 
! 
      REAL FLUXP_PP(1,3,NELEM_PP) ,FLUYP_PP(1,3,NELEM_PP) 
      REAL UMEAN_PP(NELEM_PP)
      INTEGER VSPACE_FIRST,VSPACE_LAST
      REAL DISNF_PP(NNODE,VSPACE_FIRST:VSPACE_LAST,NELEM_PP)
      REAL RHS_PP(1,3,NELEM_PP),UNKEL(3),FXEL(3),FYEL(3) 
      REAL GEOME_PP(NGEOM,NELEM_PP) 
      REAL DIVE,FXME        ,FYME  
      REAL GEO(6),MU(NNODE,NELEM_PP),NFO_PP(NNODE,NELEM_PP) 
      REAL DTE,UX,UY,C00,EQNF,NF,FREQ 
      REAL AREA 
      REAL C13,ANX,ANY 
      REAL C0,COLL 
! 
      C13=1./3. 
      C0=0.0 
! 
! *** INITIALISE UMEAN_PP 
! 
      CALL RFILLV(UMEAN_PP,NELEM_PP,C0) 
! 
! *** INITIALISE RHS_PP 
! 
      CALL RFILLA(RHS_PP,1,3,NELEM_PP,C0) 
! 
! *** LOOP OVER THE ELEMENTS 
! 
      DO 1000 IE=1,NELEM_PP 
! 
! *** TRANSFER GEOME_PP AND DISNF_PP INTO LOCAL VARIABLES AREA,GEO,UNKEL 
! 
      AREA=0.5*GEOME_PP(7,IE) 
! 
      DO 1001 IN=1,3 
      GEO(IN)=GEOME_PP(IN,IE) 
      GEO(IN+3)=GEOME_PP(IN+3,IE) 
      UNKEL(IN)=DISNF_PP(IN,IV,IE) 
 1001 CONTINUE 
! 
! *** NOW THE DIVERGENCE 
! 
      DIVE=0 
      DO 1005 IN=1,3 
      ANX=GEO(IN) 
      ANY=GEO(IN+3) 
      DIVE=DIVE+ANX*FLUXP_PP(1,IN,IE)+ANY*FLUYP_PP(1,IN,IE) 
 1005 CONTINUE 
!
! *** COMPUTE THE BGK SOURCE TERM FOR EACH ELEMENT
!
        COLL=0.0
        DO 1006 IN=1,3
        EQNF=NFO_PP(IN,IE)
        NF=DISNF_PP(IN,IV,IE)
        FREQ=MU(IN,IE)
        COLL=COLL+FREQ*(EQNF-NF)
 1006 CONTINUE
        COLL=C13*COLL

!
! *** ADD TO THE DIVERGENCE
!
       DIVE=DIVE-COLL    !CAREFUL WITH THE SIGN HERE!!!
! 
! *** MULTIPLY BY 0.5*DELTA-TIME (GLOBAL TIMESTEPPING) 
!	
      DIVE=DIVE*DTE*0.5
! 
! *** STORE IN UMEAN_PP FOR EDGE TREATMENT 
! 
      UMEAN_PP(IE)=DIVE 
! 
! *** GET THE UPDATED UNKEL 
! 
      DO 1020 IN=1,3 
      UNKEL(IN)=UNKEL(IN)-DIVE 
 1020 CONTINUE 
! 
! *** CALCULATE THE ELEMENTS CONTRIBUTION TO THE SECOND STEP 
! *** (THE SECOND TERM ON THE RHS_PP OF EQ. 2.5.1) 
! 
! *** GET THE FLUXES 
! 
      CALL GETFLU(3,UNKEL,FXEL,FYEL,UX,UY) 
! 
! *** GET THE MEAN FLUXES AND ADD BOUNDARY TERMS IF REQUIRED 
! 
      FXME=0 
      FYME=0 
      DO 1040 IN=1,3 
      FXME=FXME+FXEL(IN) 
      FYME=FYME+FYEL(IN) 
 1040 CONTINUE 
! 
      FXME=C13*FXME      !MEAN ELEMENT X-FLUX 
      FYME=C13*FYME      !MEAN ELEMENT Y-FLUX 
! 
! *** NOW COMPUTE THE RHS_PP INCLUDING THE BGK SOURCE TERM FOR EACH DISCONTINUOUS NODE
! 
      DO 1070 IN=1,3
      ANX=GEO(IN)*AREA 
      ANY=GEO(IN+3)*AREA 
      RHS_PP(1,IN,IE)=RHS_PP(1,IN,IE)+ANX*FXME+ANY*FYME 
 1070 CONTINUE
! 
! *** END LOOP OVER THE ELEMENTS 
! 
 1000 CONTINUE
! 
! *** RETURN TO ADVNCE 
! 
      RETURN 
      END 
